set verbose off

#==============================================
# Replicate Ch. 10.6 - Lagged predictors
# https://otexts.com/fpp3/lagged-predictors.html
#==============================================
open insurance.gdt

# Time-series plots
tsplots Quotes TVadverts --output=display

# Add 20 future obs
series trainset = 1
dataset addobs 20

# Set future monthly advertising to 8 units
smpl ($tmax-19) $tmax   # switch to "future" range
series trainset = 0
series TVadverts = 8

# Restrict dataset back to original data
smpl trainset == 1 --restrict --replace

# Optimal lag length for ARMA model conditional
# on lag length for TVadverts
scalar aic = $huge
matrix pq = mshape(NA,2,1)
scalar optimal_lag_x = NA
loop i=1..3
    arima 3 0 3 ; Quotes const TVadverts(0 to -$i) --lagselect --quiet

    matrix test = $test
    scalar opt = iminc(test[,3])
    scalar aic_new = test[opt,3]
    print aic_new
    if aic > aic_new  # new AIC is lower
        aic = aic_new
        pq = test[opt,1:2]
        optimal_lag_x = $i
    endif
endloop

printf "Optimal ARMA(%d,%d) (AIC=%0.3g)\n", pq[1], pq[2], aic
printf "Conditional max. lag for 'x' = %d\n", optimal_lag_x

# Test ARIMA
# Note: In the book, they estimate an ARIMA(1,0,2) instead
# but we rely on ARIMA(1,0,1) here according to the AIC
scalar opt_p = pq[1]
scalar opt_q = pq[2]
arima opt_p 0 opt_q ; Quotes const TVadverts(0 to -optimal_lag_x)

# Compute Forecast
fcast --dynamic --out-of-sample --quiet
smpl full
series fc = $fcast
series fcse = $fcse

# Create forecast plot 
scalar critical_80_pct = critical(t, $nobs, 0.1)
scalar critical_95_pct = critical(t, $nobs, 0.025)

bundle b1 = _(center="fc", width="fcse", factor=critical_80_pct, style="fill",
              color="#DCE70D2D", title="80% interval")
bundle b2 = _(center="fc", width="fcse", factor=critical_95_pct, style="fill",
              color="#DC1763E6", title="95% interval")
bundles B = defarray(b1, b2)

gnuplot Quotes fc --time-series --with-lines --bands=B --output=display \
 {set grid; set nokey;
  set title "Forecast quotes with future advertising set to 8";
  set xlabel "Month"; set ylabel "Quotes";}




