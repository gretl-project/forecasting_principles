set verbose off
# Replicate
# https://otexts.com/fpp3/useful-predictors.html#example-australian-quarterly-beer-production-1

# =============
# Exercise 1: Load data and set sample
# =============

# Requires local file
open aus_production.csv

# Alternatively, open dataset from URL:
# open "https://raw.githubusercontent.com/gretl-project/forecasting_principles/refs/heads/main/data/csv/aus_production.csv"

setobs 4 1956:1 --time-series

# Remove all observations before 1992:1 permanently
smpl obs >= 1992:1 --restrict --permanent

# Plot the target series using all data
gnuplot Beer --with-lines --time-series --output=display

# Create seasonal dummies
list Seasonals = seasonals(1)  # omit first quarter
print Seasonals -o --range=1:8
# Create a linear time trend
genr time

# =============
# Exercise 2: Linear trend model
# =============
ols Beer const time
series uhat_trendmodel = $uhat  # residuals from trend model
series yhat_trendmodel = $yhat  # fitted values from trend model

# Plot actual vs. fitted values over time
gnuplot Beer yhat_trendmodel --with-lines --time-series --output=display \
  {set grid;
   set key outside below;
   set linetype 1 lw 1.0 lc rgb "black";\
   set linetype 2 lw 1.5 lc rgb "#D55E00";
   set title "Australian quarterly beer production (trend model)";
   set ylabel "Megalitres";}

# Scatterplot with quarters as factor
series quarter = $obsminor  # series indicating quarter
print quarter -o --range=1:6

gnuplot yhat_trendmodel Beer quarter --factorized --output=display \
  {set grid;
   set key outside below;
   set title "Australian beer production";
   set ylabel "Megalitres";
   set yrange[300:550];
   set xrange[350:550];}


# =============
# Exercise 3: Seasonal model
# =============

# Unconditional means of Beer grouped by quarter
series quarter = $obsminor
print quarter -o --range=1:8
eval aggregate(Beer, quarter, "mean")

# Run regression
ols Beer const time Seasonals
series uhat_seasonmodel = $uhat
series yhat_seasonmodel = $yhat

# Plot actual vs. fitted values over time
gnuplot Beer yhat_seasonmodel --with-lines --time-series --output=display \
  {set grid;
   set key outside below;
   set linetype 1 lw 1.0 lc rgb "black";\
   set linetype 2 lw 1.5 lc rgb "#D55E00";
   set title "Australian quarterly beer production (seasonal model)";
   set ylabel "Megalitres";}

# Scatterplot with quarters as factor
gnuplot yhat_seasonmodel Beer quarter --factorized --output=display \
  {set grid;
   set key outside below;
   set title "Australian beer production";
   set ylabel "Megalitres";
   set yrange[300:550];
   set xrange[350:550];}


# =============
# Exercise 4: Fourier terms model
# =============
# pkg install basis_functions  # install package from server if not already installed
# Load package
include basis_functions.gfn

scalar K = 2
list FourierTerms = fourier(K)
tsplots FourierTerms --output=display

ols Beer const time FourierTerms
series uhat_fourier = $uhat
series yhat_fourier = $yhat

# Plot actual vs. fitted values over time
gnuplot Beer yhat_fourier --with-lines --time-series --output=display \
  {set grid;
   set key outside below;
   set linetype 1 lw 1.0 lc rgb "black";\
   set linetype 2 lw 1.5 lc rgb "#D55E00";
   set title "Australian quarterly beer production (Fourier model)";
   set ylabel "Megalitres";}


# =============
# Exercise 5: Out-of-sample forecast
# =============
smpl 1992:1 2008:4  # Define training sample

ols Beer const time Seasonals
fcast forecast_seasonmodel --out-of-sample
setinfo forecast_seasonmodel --graph-name="Seasonal Model" # for plot legend

# Compare out-of-sample forecast with actual values
smpl 2007:1 $tmax  # Define evaluation sample

list L = Beer forecast_seasonmodel
gnuplot L --time-series --with-lines --output=display \
  {set grid; set key outside below;
   set linetype 1 lw 1.0 lc rgb "black";
   set linetype 2 lw 1.5 lc rgb "red";}

# Comparison forecast errors
series fcerror_seasonmodel = Beer - forecast_seasonmodel

gnuplot fcerror_seasonmodel--time-series --with-lines --output=display
  {set grid;}

# Distribution of forecast errors
boxplot fcerror_seasonmodel --output=display

# Evaluation statistics
list L = forecast_seasonmodel
smpl L --no-missing

matrix fc_stats = fcstats(Beer, L)
cnameset(fc_stats, defarray("Season"))
print fc_stats



##################
# Repliziere
# https://otexts.com/fpp3/forecasting-regression.html#example-australian-quarterly-beer-production-2
##################

smpl 1992:1 $tmax
# Run regression
ols Beer const time Seasonals

# Add 8 new observations
dataset addobs 8

# Create forecast and standard deviation of forecast
fcast forecast --out-of-sample
series forecast_sd = $fcse
print forecast forecast_sd --byobs

# Create foreasting plot with prediction intervals
bundle b2 = _(center="forecast", width="forecast_sd", factor=1.96, style="fill")
b2.title = "95% interval"

gnuplot Beer forecast --time-series --with-lines --band=b2 \
 {set grid; set key outside below;}
